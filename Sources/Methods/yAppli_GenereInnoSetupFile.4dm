//%attributes = {}
  // ----------------------------------------------------
  // Nom utilisateur (OS) : Patrick EMANUEL
  // Date et heure : 09/08/11
  // ----------------------------------------------------
  // Méthode : _Appli_GenereInnoSetupile
  // Description
  // Les informations concernant le fichier scriptable sont données au fur et à mesure en tant que commentaires
  //
  // Paramètres
  // ----------------------------------------------------

C_TIME:C306($vDoc)
C_TEXT:C284($vnom;$1;$vChemin;$2;$Welcome)
ARRAY TEXT:C222($Inst;0)

$vNom:=<>SoftName+"_install."
$CR:=Char:C90(13)+Char:C90(10)
$Guil:=<>ZGuil

If (Count parameters:C259=2)  //Permet la gestion de test :-)
	$vChemin:=$1  // Pour les besoin du test
	$Welcome:=$2
Else 
	$vChemin:=Get 4D folder:C485(Dossier base:K5:14)+<>SoftName+"_build"+<>ZSeparateur+"Final Application"+<>ZSeparateur+<>SoftName
	$Welcome:=$Guil+"Cet assistant va vous guider pendant l'installation de "+<>SoftName+" "+String:C10(afVersion )+", la nouvelle génération de gestionnaire d'Association.\r\n\r\n"
	$Welcome:=$Welcome+"Parce que gérer une Association demande souvent du temps, "+<>SoftName+" vous libère du temps afin que vous vous consacriez à ce qui est vraiment important pour votre Association !\r\n\r\n"+$Guil+$CR
	
End if 

  //Création du jeu d'instruction
  // la commande Name permet de spécifier le nom du logiciel/produit installé
APPEND TO ARRAY:C911($Inst;";Script generated by 4D - "+<>softname+" for the Inno Setup."+$CR)
APPEND TO ARRAY:C911($Inst;";SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!"+$CR+$CR)

$Build:=String:C10(Year of:C25(<>zdatedujour))+String:C10(Month of:C24(<>zdatedujour))+String:C10(Day of:C23(<>zdatedujour))

APPEND TO ARRAY:C911($Inst;"#define MyAppName "+$Guil+<>SoftName+$Guil+$CR)
APPEND TO ARRAY:C911($Inst;"#define MyAppVersion "+$Guil+String:C10(afVersion )+"("+$Build+")"+$Guil+$CR)
APPEND TO ARRAY:C911($Inst;"#define MyAppPublisher "+$Guil+<>SoftName+$Guil+$CR)
APPEND TO ARRAY:C911($Inst;"#define MyAppExeName "+$Guil+"Install inno-"+<>SoftName+$Guil+$CR)

APPEND TO ARRAY:C911($Inst;"[Setup]"+$CR)
APPEND TO ARRAY:C911($Inst;";NOTE: The value of AppId uniquely identifies this application."+$CR)
APPEND TO ARRAY:C911($Inst;";Do not use the same AppId value in installers for other applications."+$CR)
APPEND TO ARRAY:C911($Inst;";(To generate a new GUID, click Tools | Generate GUID inside the IDE.)"+$CR)
APPEND TO ARRAY:C911($Inst;"AppId={{AF1DA5AB-C135-4FB5-B4DD-28A6C73845E4}"+$CR)
APPEND TO ARRAY:C911($Inst;"AppName={#MyAppName}"+$CR)
APPEND TO ARRAY:C911($Inst;"AppVersion={#MyAppVersion}"+$CR)
APPEND TO ARRAY:C911($Inst;"AppVerName={#MyAppName}{#MyAppVersion}"+$CR)
APPEND TO ARRAY:C911($Inst;"AppPublisher={#MyAppPublisher}"+$CR)
APPEND TO ARRAY:C911($Inst;"DefaultDirName={pf}\\{#MyAppName}"+$CR)
APPEND TO ARRAY:C911($Inst;"DefaultGroupName={#MyAppName}"+$CR)
APPEND TO ARRAY:C911($Inst;"AllowNoIcons=yes"+$CR)
APPEND TO ARRAY:C911($Inst;"OutputDir="+Get 4D folder:C485(Dossier base:K5:14)+<>SoftName+"_build"+$CR)
APPEND TO ARRAY:C911($Inst;"OutputBaseFilename={#MyAppExeName}"+$CR)
APPEND TO ARRAY:C911($Inst;"WizardImageFile="+$Guil+$vChemin+"\\wizard.bmp"+$Guil+$CR)
APPEND TO ARRAY:C911($Inst;"WizardSmallImageFile="+$Guil+$vChemin+"\\wizardsmall.bmp"+$Guil+$CR)

APPEND TO ARRAY:C911($Inst;"LicenseFile="+$Guil+$vChemin+"\\disclaimer.txt"+$Guil+$CR)
  //AJOUTER A TABLEAU($Inst;"InfoBeforeFile=CHEMIN DU FICHIER"+$CR)
  //AJOUTER A TABLEAU($Inst;"InfoAfterFile=CHEMIN DU FICHIER"+$CR)
APPEND TO ARRAY:C911($Inst;"SolidCompression=yes"+$CR+$CR)

APPEND TO ARRAY:C911($Inst;"[Languages]"+$CR)
APPEND TO ARRAY:C911($Inst;"Name:"+$Guil+"french"+$Guil+";MessagesFile:"+$Guil+"compiler:Languages\\French.isl"+$Guil+$CR+$CR)

APPEND TO ARRAY:C911($Inst;"[Tasks]"+$CR)
APPEND TO ARRAY:C911($Inst;"Name:"+$Guil+"desktopicon"+$Guil+";Description:"+$Guil+"{cm:CreateDesktopIcon}"+$Guil+";GroupDescription:"+$Guil+"{cm:AdditionalIcons}"+$Guil+";Flags: unchecked"+$CR)
APPEND TO ARRAY:C911($Inst;"Name:"+$Guil+"quicklaunchicon"+$Guil+";Description:"+$Guil+"{cm:CreateQuickLaunchIcon}"+$Guil+";GroupDescription:"+$Guil+"{cm:AdditionalIcons}"+$Guil+";Flags: unchecked"+$CR+$CR)

APPEND TO ARRAY:C911($Inst;"[Files]"+$CR)
APPEND TO ARRAY:C911($Inst;"Source:"+$Guil+$vChemin+"\\*"+$Guil+";DestDir:"+$Guil+"{app}"+$Guil+";Flags: ignoreversion recursesubdirs createallsubdirs"+$CR+$CR)

APPEND TO ARRAY:C911($Inst;"[Icons]"+$CR)
APPEND TO ARRAY:C911($Inst;"Name:"+$Guil+"{group}\\{#MyAppName}"+$Guil+"; Filename: "+$Guil+"{app}\\{#MyAppName}"+$Guil+";IconFilename: "+$Guil+"{app}\\"+<>SoftName+".ico"+$Guil+$CR)
APPEND TO ARRAY:C911($Inst;"Name:"+$Guil+"{group}\\{cm:UninstallProgram,{#MyAppName}}"+$Guil+";Filename:"+$Guil+"{uninstallexe}"+$Guil+$CR)
APPEND TO ARRAY:C911($Inst;"Name:"+$Guil+"{commondesktop}\\{#MyAppName}"+$Guil+";Filename:"+$Guil+"{app}\\{#MyAppName}"+$Guil+";Tasks: desktopicon"+$CR)
  //AJOUTER A TABLEAU($Inst;"Name:"+$Guil+"{userappdata}\\Microsoft\\Internet Explorer\\Quick Launch\\{#MyAppName}"+$Guil+";Filename:"+$Guil+"{app}\\{#MyAppName}"+$Guil+";Tasks: quicklaunchicon"+$CR+$CR)

APPEND TO ARRAY:C911($Inst;"[Run]"+$CR)
APPEND TO ARRAY:C911($Inst;"Filename:"+$Guil+"{app}\\{#MyAppName}"+$Guil+";Description:"+$Guil+"{cm:LaunchProgram,{#StringChange(MyAppName, "+$Guil+" & "+$Guil+", "+$Guil+" & & "+$Guil+")}}"+$Guil+";Flags: nowait postinstall skipifsilent"+$CR)

  //********************************************************************************
  //Balançons tout dans le fichier ".ISS"
$vDoc:=Create document:C266($vnom+"iss";"TEXT")
USE CHARACTER SET:C205("ISO_8859-1";0)
If (OK=1)
	
	For ($a;1;Size of array:C274($Inst))
		SEND PACKET:C103($vDoc;$Inst{$a})
	End for 
	
	CLOSE DOCUMENT:C267($vDoc)  // Fermer le document 
	
End if 
USE CHARACTER SET:C205(*;0)


  //Génération du fichier Application !
$Nom:=Get 4D folder:C485(Dossier base:K5:14)+$vNom+"exe"
If (Test path name:C476($Nom)=Est un document:K24:1)
	DELETE DOCUMENT:C159($Nom)
End if 

  //Si (Nombre de parametres=2)  // Permet de ne pas être bloqué lors de tests
If (True:C214)
	SET ENVIRONMENT VARIABLE:C812("_4D_OPTION_BLOCKING_EXTERNAL_PROCESS";"false")
	$appli:=Get 4D folder:C485(Dossier base:K5:14)+"Inno Setup 5"+<>ZSeparateur+"compil32 /cc"+" "+$Guil+document+$Guil
	LAUNCH EXTERNAL PROCESS:C811($appli)
	SET ENVIRONMENT VARIABLE:C812("_4D_OPTION_BLOCKING_EXTERNAL_PROCESS";"")
End if 