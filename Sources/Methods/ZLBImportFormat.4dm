//%attributes = {}
  // ----------------------------------------------------
  // User name (OS) : Bertrand SOUBEYRAND
  // Date and time : 05/07/10, 11:39:48
  // ----------------------------------------------------
  // Method : LBImportFormat
  // Description
  // This method reads the XML file generated by the 4DLB_Export_Format method
  // Inserts columns, sizes width, etc.
  //
  // Parameters
  //  $1 Text, XML file pathname
  // $2 Pointer, pointer to a text variable giving the explanation of the error code

  // Syntax : $L_Error:=4DLB_Import_Format ($T_File;->$T_TextError)

  // ----------------------------------------------------


C_TEXT:C284($1;$T_Path)
C_POINTER:C301($2)
C_LONGINT:C283($0;$L_MyError)

C_LONGINT:C283($L_Left;$L_Top;$L_Width_LB;$L_Height_LB;$L_Display_listbox_header;$L_Display_hor_listb_scrollbar;$L_Display_ver_listb_scrollbar;$L_Height_listbox_header;$L_Height_hor_listbox_scrollbar;$L_Width_ver_listbox_scrollbar;$L_Position_hor_listbx_scrollbar;$L_Position_ver_listbx_scrollbar;$L_HeightLBRow)
C_LONGINT:C283($L_Button_align;$L_Button_font_size;$L_Button_font_style;$L_Column_width_min;$L_Column_width_max;$L_Column_Table;$L_Column_Field)
C_LONGINT:C283($L_Column_font_size;$L_Column_font_style;$L_Column_width;$L_Order;$L_Column_align;$i;$L_LB_Source_table)
C_TEXT:C284($T_Root;$T_ElemName;$T_Button_object_name;$T_Button_variable_name;$T_Button_title;$T_Button_font;$T_LB_Object_name;$T_Message)
C_TEXT:C284($T_Column_formula;$T_Column_object_name;$T_Column_variable_name;$T_Column_visible;$T_Column_font;$T_Column_format;$T_Ref;$T_Ref2;$T_ColumnRef2;$T_Enterable)
C_POINTER:C301($P_Col;$P_Button)


$T_Path:=$1
$L_MyError:=1

Case of 
	: (Test path name:C476($T_Path)#Est un document:K24:1)
		
		  //  No error: we use the default columns
		
	Else 
		
		$T_Root:=DOM Parse XML source:C719($T_Path)
		If (OK#1)
			
			$L_MyError:=-2
		Else 
			
			$T_Ref2:=DOM Find XML element:C864($T_Root;"/Root/listbox")
			
			$T_Ref:=DOM Get XML element:C725($T_Ref2;"object_name_lb";1;$T_LB_Object_name)
			$T_Ref:=DOM Get XML element:C725($T_Ref2;"position_left_px";1;$L_Left)
			$T_Ref:=DOM Get XML element:C725($T_Ref2;"position_top_px";1;$L_Top)
			$T_Ref:=DOM Get XML element:C725($T_Ref2;"width_px";1;$L_Width_LB)
			$T_Ref:=DOM Get XML element:C725($T_Ref2;"height_px";1;$L_Height_LB)
			$T_Ref:=DOM Get XML element:C725($T_Ref2;"Display_header";1;$L_Display_listbox_header)  // getter but not setter in 12.0
			$T_Ref:=DOM Get XML element:C725($T_Ref2;"display_horiz_scrollbar";1;$L_Display_hor_listb_scrollbar)
			$T_Ref:=DOM Get XML element:C725($T_Ref2;"display_vert_scrollbar";1;$L_Display_ver_listb_scrollbar)
			$T_Ref:=DOM Get XML element:C725($T_Ref2;"header_height";1;$L_Height_listbox_header)  // getter but not setter in 12.0
			$T_Ref:=DOM Get XML element:C725($T_Ref2;"height_horiz_scrollbar";1;$L_Height_hor_listbox_scrollbar)
			$T_Ref:=DOM Get XML element:C725($T_Ref2;"height_vert_scrollbar";1;$L_Width_ver_listbox_scrollbar)
			$T_Ref:=DOM Get XML element:C725($T_Ref2;"position_horiz_scrollbar";1;$L_Position_hor_listbx_scrollbar)
			$T_Ref:=DOM Get XML element:C725($T_Ref2;"position_vert_scrollbar";1;$L_Position_ver_listbx_scrollbar)
			$T_Ref:=DOM Get XML element:C725($T_Ref2;"row_height";1;$L_HeightLBRow)
			$T_Ref:=DOM Get XML element:C725($T_Ref2;"source_table_lb";1;$L_LB_Source_table)
			
			LISTBOX SET ROWS HEIGHT:C835(*;$T_LB_Object_name;$L_HeightLBRow)
			OBJECT SET SCROLLBAR:C843(*;$T_LB_Object_name;($L_Display_hor_listb_scrollbar=1);($L_Display_ver_listb_scrollbar=1))
			
			If (Is table number valid:C999($L_LB_Source_table))
				LISTBOX SET TABLE SOURCE:C1013(*;$T_LB_Object_name;$L_LB_Source_table)
			End if 
			
			
			  // We delete any columns drawn in the structure 
			  // before adding them according to the XML file description
			LISTBOX DELETE COLUMN:C830(*;$T_LB_Object_name;1;LISTBOX Get number of columns:C831(*;$T_LB_Object_name))
			
			For ($i;1;DOM Count XML elements:C726($T_Ref2;"column"))
				
				$T_Ref:=DOM Find XML element:C864($T_Root;"/Root/listbox/column["+String:C10($i)+"]")
				
				$T_ColumnRef2:=DOM Get first child XML element:C723($T_Ref;$T_ElemName;$T_Button_object_name)
				$T_ColumnRef2:=DOM Get next sibling XML element:C724($T_ColumnRef2;$T_ElemName;$T_Button_variable_name)
				$T_ColumnRef2:=DOM Get next sibling XML element:C724($T_ColumnRef2;$T_ElemName;$T_Button_title)
				$T_ColumnRef2:=DOM Get next sibling XML element:C724($T_ColumnRef2;$T_ElemName;$L_Button_align)
				$T_ColumnRef2:=DOM Get next sibling XML element:C724($T_ColumnRef2;$T_ElemName;$T_Button_font)
				$T_ColumnRef2:=DOM Get next sibling XML element:C724($T_ColumnRef2;$T_ElemName;$L_Button_font_size)
				$T_ColumnRef2:=DOM Get next sibling XML element:C724($T_ColumnRef2;$T_ElemName;$L_Button_font_style)
				
				$T_ColumnRef2:=DOM Get next sibling XML element:C724($T_ColumnRef2;$T_ElemName;$T_Column_object_name)
				$T_ColumnRef2:=DOM Get next sibling XML element:C724($T_ColumnRef2;$T_ElemName;$T_Column_variable_name)
				
				$T_ColumnRef2:=DOM Get next sibling XML element:C724($T_ColumnRef2;$T_ElemName;$L_Column_Table)
				$T_ColumnRef2:=DOM Get next sibling XML element:C724($T_ColumnRef2;$T_ElemName;$L_Column_Field)
				
				$T_ColumnRef2:=DOM Get next sibling XML element:C724($T_ColumnRef2;$T_ElemName;$T_Column_formula)  //  impossible in v12.0, no formula exported
				
				$T_ColumnRef2:=DOM Get next sibling XML element:C724($T_ColumnRef2;$T_ElemName;$T_Column_visible)
				$T_ColumnRef2:=DOM Get next sibling XML element:C724($T_ColumnRef2;$T_ElemName;$L_Order)
				$T_ColumnRef2:=DOM Get next sibling XML element:C724($T_ColumnRef2;$T_ElemName;$L_Column_align)
				$T_ColumnRef2:=DOM Get next sibling XML element:C724($T_ColumnRef2;$T_ElemName;$T_Column_font)
				$T_ColumnRef2:=DOM Get next sibling XML element:C724($T_ColumnRef2;$T_ElemName;$L_Column_font_size)
				$T_ColumnRef2:=DOM Get next sibling XML element:C724($T_ColumnRef2;$T_ElemName;$L_Column_font_style)
				$T_ColumnRef2:=DOM Get next sibling XML element:C724($T_ColumnRef2;$T_ElemName;$L_Column_width)
				
				$T_ColumnRef2:=DOM Get next sibling XML element:C724($T_ColumnRef2;$T_ElemName;$L_Column_width_min)
				$T_ColumnRef2:=DOM Get next sibling XML element:C724($T_ColumnRef2;$T_ElemName;$L_Column_width_max)
				
				$T_ColumnRef2:=DOM Get next sibling XML element:C724($T_ColumnRef2;$T_ElemName;$T_Column_format)
				$T_ColumnRef2:=DOM Get next sibling XML element:C724($T_ColumnRef2;$T_ElemName;$T_Enterable)
				
				
				If ($L_Column_Table=-1)
					$P_Col:=Get pointer:C304($T_Column_variable_name)
				Else 
					$P_Col:=Field:C253($L_Column_Table;$L_Column_Field)
				End if 
				
				
				$P_Button:=Get pointer:C304($T_Button_variable_name)
				LISTBOX INSERT COLUMN:C829(*;$T_LB_Object_name;$L_Order;$T_Column_object_name;$P_Col->;$T_Button_object_name;$P_Button->)
				
				OBJECT SET VISIBLE:C603(*;$T_Column_object_name;($T_Column_visible="VRAI"))
				OBJECT SET HORIZONTAL ALIGNMENT:C706(*;$T_Column_object_name;$L_Column_align)
				OBJECT SET FONT:C164(*;$T_Column_object_name;$T_Column_font)
				OBJECT SET FONT SIZE:C165(*;$T_Column_object_name;$L_Column_font_size)
				OBJECT SET FONT STYLE:C166(*;$T_Column_object_name;$L_Column_font_style)
				LISTBOX SET COLUMN WIDTH:C833(*;$T_Column_object_name;$L_Column_width;$L_Column_width_min;$L_Column_width_max)
				
				OBJECT SET FORMAT:C236(*;$T_Column_object_name;$T_Column_format)
				OBJECT SET ENTERABLE:C238(*;$T_Column_object_name;($T_Enterable="VRAI"))
				
				OBJECT SET TITLE:C194(*;$T_Button_object_name;$T_Button_title)
				OBJECT SET HORIZONTAL ALIGNMENT:C706(*;$T_Button_object_name;$L_Button_align)
				OBJECT SET FONT:C164(*;$T_Button_object_name;$T_Button_font)
				OBJECT SET FONT SIZE:C165(*;$T_Button_object_name;$L_Button_font_size)
				OBJECT SET FONT STYLE:C166(*;$T_Button_object_name;$L_Button_font_style)
			End for 
			
			DOM CLOSE XML:C722($T_Root)
		End if 
End case 


Case of 
	: ($L_MyError=-1)
		
		$T_Message:="The format XML file was not found"
		
	: ($L_MyError=-2)
		
		$T_Message:="The XML file is not valid"
		
	Else 
		$T_Message:="OK"
End case 

If (Test path name:C476($T_Path)=Est un document:K24:1)
	DELETE DOCUMENT:C159($T_Path)
End if 

$0:=$L_MyError
$2->:=$T_Message
